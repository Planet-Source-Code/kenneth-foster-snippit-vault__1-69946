Private Sub AltLVBackground(lv As ListView, _
    ByVal BackColorOne As OLE_COLOR, _
    ByVal BackColorTwo As OLE_COLOR)
'---------------------------------------------------------------------------------
' Purpose   : Alternates row colors in a ListView control
' Method    : Creates a picture box and draws the desired color scheme in it, then
'             loads the drawn image as the listviews picture.
'---------------------------------------------------------------------------------
Dim lH      As Long
Dim lSM     As Byte
Dim picAlt  As PictureBox
    With lv
        If .View = lvwReport And .ListItems.Count Then
            Set picAlt = Me.Controls.Add("VB.PictureBox", "picAlt")
            lSM = .Parent.ScaleMode
            .Parent.ScaleMode = vbTwips
            .PictureAlignment = lvwTile
            lH = .ListItems(1).Height
            With picAlt
                .BackColor = BackColorOne
                .AutoRedraw = True
                .Height = lH * 2
                .BorderStyle = 0
                .Width = 10 * Screen.TwipsPerPixelX
                picAlt.Line (0, lH)-(.ScaleWidth, lH * 2), BackColorTwo, BF
                Set lv.Picture = .Image
            End With
            Set picAlt = Nothing
            Me.Controls.Remove "picAlt"
            lv.Parent.ScaleMode = lSM
        End If
    End With
End Sub

Private Sub Form_Load()
Dim x As Long

With lv.ListItems
    For x = 1 To 50
        .Add(, "|" & x, "ListItem " & x, , 1).ListSubItems.Add , "|", "ListSubItem " & x
    Next
End With

AltLVBackground lv, vbWhite, &HC0FFFF

End Sub

Private Sub Check1_Click()
    Me.lv.GridLines = Not Me.lv.GridLines
End Sub

Private Sub Check2_Click()
    AltLVBackground lv, IIf(Me.Check2.Value, &HC0FFFF, vbWhite), IIf(Me.Check2.Value, vbWhite, &HC0FFFF)
End Sub

